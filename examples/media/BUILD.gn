# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/fidl/fidl.gni")
import("//build/package.gni")
import("//build/testing/environments.gni")

# We have a separate package for each example to clarify the dependencies of
# each example.

package("use_media_decoder") {
  testonly = true

  deps = [
    "use_media_decoder",
  ]

  binaries = [
    {
      name = "use_media_decoder"
      shell = true
    },
  ]

  meta = [
    {
      path = rebase_path("use_media_decoder/meta/use_media_decoder.cmx")
      dest = "use_media_decoder.cmx"
    },
  ]
}

#
# Tests that run on CQ
#

package("use_aac_decoder_test") {
  testonly = true

  deps = [
    "//garnet/examples/media/use_media_decoder:use_aac_decoder_test",
  ]

  tests = [
    {
      name = "use_aac_decoder_test"
      environments = basic_envs
    },
  ]

  resources = [
    {
      path = rebase_path("use_media_decoder/test/test_audio.adts")
      dest = "media_test_data/test_audio.adts"
    },
  ]

  meta = [
    {
      path = rebase_path("use_media_decoder/meta/use_aac_decoder_test.cmx")
      dest = "use_aac_decoder_test.cmx"
    },
  ]
}

#
# Tests that don't run on CQ yet.
#

package("use_h264_decoder_test") {
  testonly = true

  deps = [
    "//garnet/examples/media/use_media_decoder:use_h264_decoder_test",
  ]

  meta = [
    {
      path = rebase_path("use_media_decoder/meta/use_h264_decoder_test.cmx")
      dest = "use_h264_decoder_test.cmx"
    },
  ]

  tests = [
    {
      # Should pass when run VIM2, astro, or QEMU.
      name = "use_h264_decoder_test"

      environments = [
        vim2_env,
        astro_env,
        qemu_env,
      ]
    },
  ]

  resources = [
    {
      path = rebase_path(
              "../../test_data/media/third_party/chromium_media_test_data/bear.h264")
      dest = "bear.h264"
    },
  ]
}

package("use_vp9_decoder_test") {
  testonly = true

  deps = [
    "//garnet/examples/media/use_media_decoder:use_vp9_decoder_test",
  ]

  meta = [
    {
      path = rebase_path("use_media_decoder/meta/use_vp9_decoder_test.cmx")
      dest = "use_vp9_decoder_test.cmx"
    },
  ]

  tests = [
    {
      # Soon, this should pass when run on VIM2 or astro, and fail otherwise.
      #
      # However, at the moment VP9 HW decode is not enabled, so this will fail
      # everywhere for now.  That's why this package is not included in tests
      # yet.
      #
      # TODO(MTWN-224): Fix the above comment when VP9 HW decode is enabled.
      name = "use_vp9_decoder_test"

      environments = [
        vim2_env,
        astro_env,
      ]
    },
  ]

  resources = [
    {
      path = rebase_path(
              "../../test_data/media/third_party/chromium_media_test_data/bear-vp9.ivf")
      dest = "bear-vp9.ivf"
    },
  ]
}
